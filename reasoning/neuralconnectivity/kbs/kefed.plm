;; $Id: kefed.plm 38 2011-03-18 05:41:28Z taruss2000@gmail.com $
;;
;;  Modules and code for the kefed neuralconnectivity application
;;


(IN-PACKAGE "STELLA")

(IN-MODULE "PL-USER")

(DEFMODULE "KEFED"
    :INCLUDES ("PL-KERNEL-KB" "ATLAS" "SWANSON98" "SPECIES" "DIGITAL-LIBRARY")
    :USES ("LOGIC" "STELLA" "UNIT-KB")
    :CASE-SENSITIVE? TRUE)

(IN-MODULE "KEFED")
(IN-DIALECT :KIF)

;; To Do:  Break some of these things out into separate
;;         ontologies that are loaded/imported for kefed!

(DEFCONCEPT Organ-Type ((?type CONCEPT))
  :=> (<=> (HOLDS ?type ?concept)
	   (HOLDS SUBSET-OF ?concept Organ)))
(DEFCONCEPT Organ)
(DEFCONCEPT Adrenal-Gland (Organ)
  :AXIOMS (Organ-Type Adrenal-Gland))
(DEFCONCEPT Tissue)
(DEFCONCEPT Adrenal-Gland-Tissue (?t Tissue)
  :=> (EXISTS (?ag) (AND (Adrenal-Gland ?ag) (belongs-to ?t ?ag))))

(DEFCONCEPT Drug-Type ((?type CONCEPT))
  :=> (<=> (HOLDS ?type ?concept)
	   (HOLDS SUBSET-OF ?concept Drug)))

(DEFCONCEPT Chemical)
(DEFCONCEPT Tracer-Chemical (Chemical))
(DEFCONCEPT Retrograde-Tracer-Chemical (Tracer-Chemical))
(DEFCONCEPT Anterograde-Tracer-Chemical (Tracer-Chemical))
(DEFCONCEPT Drug (Chemical))
(DEFCONCEPT Metyrpone (Drug)
  :AXIOMS (Drug-Type Metyrpone))
(DEFCONCEPT Drug-Vehicle)

;;;
;;;  Main KEfED concepts
;;;

(DEFCONCEPT Kefed-Object ())

(DEFCONCEPT Experiment (Kefed-Object))
(DEFFUNCTION model-source ((?c CONCEPT) (?r Reference)))
(DEFFUNCTION experiment-source ((?e Experiment) (?r Reference) (?case STRING)))
(DEFFUNCTION experiment-reference ((?e Experiment) (?r Reference))
  :<= (EXISTS (?case) (experiment-source ?e ?r ?case)))

(DEFCONCEPT Kefed-Model-Element (Kefed-Object)
  :DOCUMENTATION "An element that is part of a Kefed Model.  Kefed elements
will be named using UUIDs.")
(DEFCONCEPT Activity (Kefed-Model-Element)
  :DOCUMENTATION "An Activity that is part of a Kefed Model.  This represents
something that is done during an experiment.")
(DEFCONCEPT Experimental-Object (Kefed-Model-Element)
  :DOCUMENTATION "An Experimental Object that is part of a Kefed Model. This
represents an object that is acted upon during an experiment.")

(DEFFUNCTION element-name ((?e Kefed-Object) (?name STRING))
  :DOCUMENTATION "A mapping from the Kefed object to its human-readable name.
Useful to make the object more readable when UUIDs are used to name
the objects themselves.")
(DEFFUNCTION element-named ((?n STRING) (?x Kefed-Object))
  :DOCUMENTATION "Lookup function to find Kefed objects based on their human-readable
name.  Inverse of `element-name'."
  :inverse element-name)


(DEFCONCEPT Variable (Kefed-Model-Element)
  :DOCUMENTATION "A variable in a Kefed Model.")
(DEFCONCEPT Parameter-Variable (Variable)
  :DOCUMENTATION "A variable that is set externally and serves as an input
to a Kefed workflow.")
(DEFCONCEPT Constant-Variable (Variable)
  :DOCUMENTATION "A variable which only takes on a single value for all of the
data during an experiment.  It is kept constant to eliminate its potential
influence on the outcome of the experiment.")
(DEFCONCEPT Measurement-Variable ((?d Variable))
  :DOCUMENTATION "A variable whose value is measured during the experiment.
It is the source of data in the experimment which will later be
analyzed and provides the results of running the experiment."
  :=> (EXISTS (?i) (depends-on ?d ?i)))
(DEFCONCEPT Datum ()
  :DOCUMENTATION "A particular measurement datum, which is used to combine the
measured values as well as the data context in which the measurement
occurs.")

(DEFRELATION has-variable ((?experiment Experiment) (?v Variable))
  :DOCUMENTATION "Links an experiment to the variables that are defined
for that experiment.  Includes all types of variables.")
(DEFRELATION depends-on ((?d Measurement-Variable) (?i Variable))
  :DOCUMENTATION "Links a measurement variable to all the other variables
on which it has a dependence.")
(DEFRELATION measures ((?v Variable) ?thing)
  :DOCUMENTATION "Links a variable to a description of the semantic \"thing\"
that the variable measures.  This is used to ground the variable
meaning and to allow identification of variables measuring the
same thing across different experiments.")
(DEFRELATION has-field ((?var Variable) (?field RELATION))
  :DOCUMENTATION "Links a variable to the function for extracting variable data
fields from that variable.  Present only for variables that have multiple
field values.")

(DEFRELATION belongs-to (?x ?y))

(DEFRELATION measurement-variable-datum ((?exp Experiment) (?var Measurement-Variable) (?d Datum))
  :DOCUMENTATION "Associates a datum with a particular measurement variable in
a given experiment.  There can be many datum objects that are associated
with such a variable.  Each one represents a single data point.  There is
only one experiment and measurement variable for each datum.")
(DEFFUNCTION datum-value ((?d Datum) (?var Variable) ?value)
  :DOCUMENTATION "The datum value associated with a particular variable.
The variable can be of any type.  This is the top level value
association relation.")
(DEFFUNCTION datum-measurement-value ((?d Datum) (?var Measurement-Variable) ?value)
  :DOCUMENTATION "The datum value associated with a particular measurement
variable.  Since each datum is only associated with a single measurement
variable, there will only ever be one measurement variable for the datum."
  :AXIOMS (SUBSET-OF datum-measurement-value datum-value))
(DEFFUNCTION datum-context-value ((?d Datum) (?var Variable) ?value)
  :DOCUMENTATION "The datum value associated with the variables that make
up the context for the datum-measurement-value.  These will often
be the independent variables or experimental constants for a KEfED modal."
  :AXIOMS (SUBSET-OF datum-context-value datum-value))

(DEFFUNCTION context-list ((?d Variable) (?context COLLECTION))
  :DOCUMENTATION "The collection of all the variables that a particular
variable depends on.  A convenience function that collects
all of the variables into a set."
  :<= (AND (Measurement-Variable ?d)
	   (BOUND-VARIABLES ?d)
	   (COLLECT-INTO-SET (SETOFALL (?v) (depends-on ?d ?v)) ?context)))

